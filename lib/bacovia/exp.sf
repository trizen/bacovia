#!/usr/bin/ruby

class Exp(v) {
    method *(Exp o) {
        Exp(v + o.v)
    }

    method *(Object o) {
        Product(self, o)
    }

    method /(Exp o) {
        Exp(v + o.v.neg)
    }

    method /(Object o) {
        o.inv * self
    }

    method +(Object o) {
        Sum(self, o)
    }

    method -(Object o) {
        Sum(self, o.neg)
    }

    method **(Object o) {
        Exp(v * o)
    }

    method alternatives {
        gather {
            for a in (v.alternatives) {
                take(Exp(a))

                # exp(log(a) * b) = a^b
                if (a.kind_of(Product) && (a.values.len == 2)) {
                    var (x, y) = a.values...
                    if (x.class == :Log) {
                        take(Power(x.v, y).alternatives...)
                    }
                    elsif(y.class == :Log) {
                        take(Power(y.v, x).alternatives...)
                    }
                }
            }
        }.uniq_by { .to_s }
    }

    method inv {
        Exp(v.neg)
    }

    method neg {
        Product(-1, self)
    }

    method numeric {
        v.numeric.exp
    }

    method to_s {
        "Exp(#{v})"
    }
}
